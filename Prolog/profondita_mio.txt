/**Versione con livello. Il livello viene confrontato con la profondità massima. Se siamo ancora al di sotto della profondità massima continuiamo con la ricerca*/
/**Se non vi è soluzione nella profondità corrente la profondità massima viene aumentata, di modo da riuscire prima o poi*/
/**Una soluzione (se esiste)*/

num_col(10).
num_righe(10).

occupata(pos(2,5)).
occupata(pos(3,5)).
occupata(pos(4,5)).
occupata(pos(5,5)).
occupata(pos(6,5)).
occupata(pos(7,5)).
occupata(pos(7,1)).
occupata(pos(7,2)).
occupata(pos(7,3)).
occupata(pos(7,4)).
occupata(pos(5,7)).
occupata(pos(6,7)).
occupata(pos(7,7)).
occupata(pos(8,7)).
occupata(pos(4,7)).
occupata(pos(4,8)).
occupata(pos(4,9)).
occupata(pos(4,10)).

iniziale(pos(4,2)).

finale(pos(7,9)).


applicabile(est,pos(R,C)) :-
	num_col(NC), C<NC,
	C1 is C+1,
	\+ occupata(pos(R,C1)).

applicabile(sud,pos(R,C)) :-
	num_righe(NR), R<NR,
	R1 is R+1,
	\+ occupata(pos(R1,C)).

applicabile(ovest,pos(R,C)) :-
	C>1,
	C1 is C-1,
	\+ occupata(pos(R,C1)).


applicabile(nord,pos(R,C)) :-
	R>1,
	R1 is R-1,
	\+ occupata(pos(R1,C)).

trasforma(est,pos(R,C),pos(R,C1)) :- C1 is C+1.
trasforma(ovest,pos(R,C),pos(R,C1)) :- C1 is C-1.
trasforma(sud,pos(R,C),pos(R1,C)) :- R1 is R+1.
trasforma(nord,pos(R,C),pos(R1,C)) :- R1 is R-1.

ric_prof_lim(S,_,_,_,[]) :- finale(S),!.
ric_prof_lim(S,D,CL,Visitati,[Az|Resto]) :-
    CL=<D,
    applicabile(Az,S),
    trasforma(Az,S,Nuovo_S),
    \+ member(Nuovo_S,Visitati),
    CL1 is CL+1,
    ric_prof_lim(Nuovo_S,D,CL1,[S|Visitati],Resto).

ric_prof_id(I,D,CL,Ris) :- ric_prof_lim(I,D,CL,[],Ris).
ric_prof_id(I,D,CL,Ris) :-
    D1 is D+1,
    ric_prof_id(I,D1,CL,Ris).

prof_id :- iniziale(I),ric_prof_id(I,1,1,Ris),write(Ris).