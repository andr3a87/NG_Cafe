% !TEX root = ../main.tex
\part{Clingo}

\chapter{Answer Set Programming}
In questo capitolo mostriamo le soluzioni realizzate per i problemi sui vincoli
proposti. Per risolvere questi problemi abbiamo fatto uso dell Answer Set Solver \emph{Clingo}.

\section{Problema delle Cinque Case}
In questa parte verrà descritto lo svolgimento dell'esercizio delle Cinque Case. In questo caso è stato utilizzata la versione 4.4.0 di clingo.

Il problema viene così enunciato:

\small{\begin{quote}
    Cinque persone di nazionalità diverse vivono in cinque case allineate lungo una
    strada, esercitano cinque professioni distinte, e ciascuna persona ha un animale favorito e una
    bevanda favorita, tutti diversi fra loro. Le cinque case sono dipinte con colori diversi. Sono noti i
    seguenti fatti:
    \begin{enumerate}
        \item{L'inglese vive nella casa rossa.}
        \item{Lo spagnolo possiede un cane.}
        \item{Il giapponese è un pittore.}
        \item{L'italiano beve tè.}
        \item{Il norvegese vive nella prima casa a sinistra.}
        \item{Il proprietario della casa verde beve caffè.}
        \item{La casa verde è immediatamente sulla destra di quella bianca.}
        \item{Lo scultore alleva lumache.}
        \item{Il diplomatico vive nella casa gialla.}
        \item{Nella casa di mezzo si beve latte.}
        \item{La casa del norvegese è adiacente a quella blu.}
        \item{Il violinista beve succo di frutta.}
        \item{La volpe è nella casa adiacente a quella del dottore.}
        \item{Il cavallo è nella casa adiacente a quella del diplomatico.}
    \end{enumerate}
\end{quote}}

L'obiettivo consiste nel trovare chi ha come animale domenstico la \emph{giraffa}.

\begin{lstlisting}
% houses
house_position(1..5).
house_col(red;green;blue;yellow;white).
house_per(eng;spa;jap;ita;nor).
house_prof(painter;sculptor;diplomatic;doctor;violinist;).
house_ani(dog;zebra;horse;fox;snail).
house_bev(coffee;milk;tea;juice;beer).

% (Directed) Edges
adj(1,2).
adj(2,3).
adj(3,4).
adj(4,5).

% Generate
1 { house(P,C,N,PR,A,B) : house_col(C), house_per(N), house_prof(PR), house_ani(A), house_bev(B)} 1 :- house_position(P).

% different colors
:- X != Y, house(X,C,_,_,_,_), house(Y,C,_,_,_,_).
% different nationality
:- X != Y, house(X,_,N,_,_,_), house(Y,_,N,_,_,_).
% different prof
:- X != Y, house(X,_,_,PR,_,_), house(Y,_,_,PR,_,_).
% different animals
:- X != Y, house(X,_,_,_,AN,_), house(Y,_,_,_,AN,_).
% different beverage
:- X != Y, house(X,_,_,_,_,B), house(Y,_,_,_,_,B).
% inglese rosso
:- house(_,red,P,_,_,_), P != eng.
% spagnolo possiede cane
:- house(_,_,P,_,dog,_), P != spa.
% giapponese pittore
:- house(_,_,P,painter,_,_), P != jap.
% italiano beve te
:- house(_,_,P,_,_,tea), P != ita.
% norvegese prima casa a sinistra
:- house(1,_,P,_,_,_), P != nor.
% proprietario casa verde beve caffe
:- house(_,C,_,_,_,coffee), C != green.
% casa verde destra bianca
:- house(X,white,_,_,_,_), house(Y,green,_,_,_,_), not adj(X,Y).
% scultore alleva lumache
:- house(_,_,_,P,snail,_), P != sculptor.
% diplomatico nella casa gialla
:- house(_,yellow,_,P,_,_), P != diplomatic.
% casa 3 si beve latte
:- house(3,_,_,_,_,B), B != milk.
% norv adiacente blue
:- house(2,C,_,_,_,_), C != blue.
% violinista succo di frutta
:- house(_,_,_,P,_,juice), P != violinist.
% volpe adiacente dottore
:- house(X,_,_,_,fox,_), house(Y,_,_,doctor,_,_), not adj(X,Y), not adj(Y,X).
% cavallo adiacente diplomatico
:- house(X,_,_,_,horse,_), house(Y,_,_,diplomatic,_,_), not adj(X,Y), not adj(Y,X).
\end{lstlisting}


Dalla linea 2 alla linea 7 (codice sottostante) sono stati dichiarati i fatti del problema: il numero delle case, i colori disponibili, le nazionalità, le professioni
gli animali e le bevande. Le linee dalla 10 alla 13 servono per descrivere il concetto di adiacenza delle case. La linea 16 serve per generare tutti i possibili modelli. Le linee dalla 19 alla 55 servono per descrivere i vincoli (\emph{integrity constraint}) che il nostro modello dovrà soddisfare.

In particolare i vincoli per assegnare ogni casa consistono di:

\begin{enumerate}
\item color: non possono esserci case diverse con colore uguale (riga 19).
\item nationality: non possono esistere due case diverse il cui abitante ha la stessa nazionalità (riga 21).
\item profession: non possono esserci due case diverse con uguale professione (riga 23).
\item animal: stesso vincolo di sopra ma prendendo in considerazione gli animali (riga 27).
\item beverage: stesso vincolo di sopra ma prendendo in considerazione le bevande (riga 27).
\end{enumerate}

Dalla riga 28 alla 55 vengono descritti i vincoli specifici dei fatti conosciuti.

Il concetto generale qui applicato consiste nel creare un solo predicato contenente tutti i termini che descrivono una singola casa. In particolare il predicato $house$ è definito nel seguente modo:

\begin{lstlisting}
house(id_position,
      color,
      resident_nationality,
      resident_profession,
      resident_pet,
      resident_best_beverage)
\end{lstlisting}

I termini del predicato dovrebbero essere auto-esplicanti. Nel codice i vincolo sono espressi tramite l'uso della sintassi con underscore (es. $pred(N,_)$), che equivale ad una wild-card.

Di seguito la soluzione trovata dal programma:
\begin{lstlisting}
house(1,yellow,nor,diplomatic,fox,beer)
house(2,blue,ita,doctor,horse,tea)
house(3,red,eng,sculptor,snail,milk)
house(4,white,spa,violinist,dog,juice)
house(5,green,jap,painter,zebra,coffee)
\end{lstlisting}

\section{Pianificazione tramite Answer Set Programming}

Un approccio alla pianificazione è basato sulla verifica di soddisfacibilità di una formula proposizionale, che rappresenta il problema di pianificazione.
In ASP il problema viene modellato tramite regole della programmazione proposizionale, che ammette la negazione per fallimento. Questo approccio (oltre a SATPLAN) ha una affinità con i grafi di pianificazione descritti da \emph{GRAPHPLAN}.

Un grafo di pianificazione in \emph{GRAPHPLAN} è un grafo orientato organizzato in livelli: $S_0, A_0, S_1, A_1, ... , A_2, S_{n+1}$.

Il livello $S_i$ è l'insieme dei nodi che rappresentano fluenti validi in $S_i$. Il livello $A_i$ è l'insieme dei nodi che rappresentano le azioni applicabili in $S_i$. Ci sarà quindi un'alternanza di livelli $S_i$ e $A_i$, fino alla condizione di terminazione. Lo stato $S_{n+1}$ rappresenta lo stato che contiene il goal.

Per rappresentare il problema di pianificazione attraverso \emph{GRAPHPLAN} abbiamo bisogno di due predicati che, rispettivamente, rappresentino i fluenti (sia veri che falsi) e le azioni eseguite in ogni stato.

\begin{enumerate}
\item \emph{holds(F,S)}: in cui F è un fluente ed S è il livello $S_i$, in F è valido.
\item \emph{occurs(A,L)}: che indica che una action A è eseguito nel livello $L$ (in questo il livello L è un $A_i$).
\end{enumerate}

In clingo i fluenti enumerabili sono stati formulati attraverso il predicato \emph{fluent(F) :- proposition(term)}, mentre le azioni possibili nel dominio sono state formulate attraverso il predicato \emph{action(A) :- proposition(term)}.

Abbiamo inoltre introdotto un costrutto sintattico per generare tutte le possibili \emph{occurs(A,L)} e permettere più azioni possibili contemporaneamente.

\begin{lstlisting}
1{occurs(A,L): action(A)} :- level(L).
\end{lstlisting}

Gli \emph{Effetti} delle \emph{occurs(A,L)}, ossia delle azioni, sono stati descritti tramite la seguente regola che esprime l'aggiunta di un fluente allo stato successivo.

\begin{lstlisting}
holds(F,S+1) :- occurs(A,S), state(S)
\end{lstlisting}

I mutex sono stati espressi nella forma di una \emph{regola false}, ossia:

\begin{lstlisting}
:- occurs(A,S), holds(F1,S), holds(F2,S), ..., holds(FN,S), state(S)
\end{lstlisting}

Per rappresentare le regole di persistenza (dette anche no-op), possiamo utilizzare delle regole non monotone espresse nel modo seguente:

\begin{lstlisting}
holds(F, S+1) :-
  fluent(F), state(S),
  holds(F,S), not -holds(F,S+1).
\end{lstlisting}

Queste regole ``copiano'' un fluente dallo stato S allo stato S+1 se e solo se nello stato S è presente il fluente F ma non la sua negazione nello stato S+1.

La regola di persistenza è usata anche per rendere persistenti i predicati \emph{-holds(F,S)}, che esprimono una negazione forte.

Il goal viene formulato tramite il vincolo:

\begin{lstlisting}
goal :- descrizione stato finale
:- not goal
\end{lstlisting}

Poiché dobbiamo mantenere gli stati completi, ossia mantengono $F$ o $\neg{F}$ per ogni fluent $F$, dobbiamo aggiungere le regole causali. Queste regole asseriscono i predicati \emph{-holds{F,S}} se nello stato S non è presente il fluente F o se non è possibile dedurlo.

\section{Problema dell'Air Cargo}

Il codice sottostante illustra come il problema di pianificazione air cargo, descritto nel paragrafo 10.1.1 del \emph{Russel-Norvig}, possa essere implementato mediante l’utilizzo della ASP.

Nella \emph{prima parte} del codice (righe 1-4) troviamo la generazione dei predicati che descrivno gli stati e i livelli.

Nella \emph{seconda parte} troviamo, le \emph{azioni eseguibili} e i \emph{fluenti ammessi} nel dominio:

\begin{enumerate}
\item le azioni eseguibili (8,9,10)
\begin{enumerate}
\item load(C,P,A): carica un cargo $C$ all'interno dell'aereo $P$ da un aeroporto $A$.
\item unload(C,P,A): scarica una cargo $C$ dall'aereo $P$ in un aeroporto (A).
\item fly(P,A1,A2): sposta l'aereo P dall'aeroporto A1 all'aeroporto A2.
\end{enumerate}

\item la generazione delle \emph{occurs(F,S)}, che consente più azioni contemporaneamente (riga 13)

\item i fluenti esistenti
\begin{enumerate}
\item in(C,P): il cargo $C$ è caricato nell'aereo $P$.
\item at(P,A): il plane $P$ è nell'aeroporto $A$.
\item at(C,A): il cargo $C$ è nell'aeroporto $A$.
\end{enumerate}
\end{enumerate}

Nella \emph{terza parte} definiamo gli effetti delle azioni, le precondizioni, le regole di persistenza e le regole causali:

\begin{enumerate}
\item gli effetti delle azioni (28-32)
\begin{enumerate}
\item load: nello stato $S+1$, il cargo $C$ è nell'aereo $P$ se viene effettuata una azione di load.
\item unload: nello stato $S+1$, il cargo $C$ è nell'aeroporto $A$ se viene effettuata una azione di unload.
\item fly: l'aereo è in aeroporto A2 se viene effettuata una azione di fly.
\end{enumerate}

\item precondizioni delle azioni load, unload e fly:
\begin{enumerate}
\item load: non è possibile applicare l'azione di load se il cargo $C$ si trova in qualche altro aereo $P_1$ o se il cargo $C$ e l'aereo $P$ non si trovano nello stesso aeroporto $A$.
\item unload: non è possibile applicare l'azione di unload se il cargo $C$ non è all'interno dell'aereo $P$ e se l'aereo $P$ non si trova nell'aeroporto $A$.
\item fly: non è possibile applicare l'azione di fly se l'aereo $P$ non si trova nell'aeroporto $A1$
\end{enumerate}

\item regole di persistenza: svolgono il compito delle azioni di \emph{no-op}, già spiegate nella parte introduttiva.

\item regole causali

\end{enumerate}

Nella \emph{quarta e ultima parte} definiamo lo stato iniziale, la descrizione del goal e le istruzioni di post-processing per specifiche di clingo.

Qui di seguito il codice completo:

\begin{lstlisting}
#const lastlev=300.

level(0..lastlev).
state(0..lastlev+1).

% AZIONI

action(load(C,P,A)) :- cargo(C), plane(P), airport(A).
action(unload(C,P,A)) :- cargo(C), plane(P), airport(A).
action(fly(P,A1,A2)) :- plane(P), airport(A1), airport(A2), A1 != A2.

% le azioni possono essere eseguite in parallelo
1{occurs(A,L): action(A)} :- level(L).

% FLUENTI

% cargo C is in plane P
fluent(in(C,P))  :- cargo(C), plane(P).
% cargo C is at airport A
fluent(at(C,A))  :- cargo(C), airport(A).
% plane P is at airport A
fluent(at(P,A))  :- plane(P), airport(A).

% EFFETTI
% i -holds vengono generati dalle regole causali

% afferma che un cargo e' su un aereo, quando viene caricato
holds(in(C,P),S+1) :- occurs(load(C,P,A),S),state(S).
% rimette i cargo in un areoporto quando vengono scaricati
holds(at(C,A),S+1) :- occurs(unload(C,P,A),S),state(S).
% mette l'aereo nel nuovo areoporto quando l'azione fly deve essere fatta
holds(at(P,AD),S+1) :- occurs(fly(P,AS,AD),S),state(S).

% PRECONDIZIONI

% precondizioni di laod
% - il cargo non deve essere su un aereo
% - il cargo e l'aereo devono essere nello stesso areoporto
:- occurs(load(C,P0,A),S), holds(in(C,P1),S).
:- occurs(load(C,P,A),S), -holds(at(C,A),S).
:- occurs(load(C,P,A),S), -holds(at(P,A),S).

% precondizioni di unload
% - il cargo deve essere su quell'aereo, l'aereo in quell'areoporto
:- occurs(unload(C,P,A),S), -holds(in(C,P),S).
:- occurs(unload(C,P,A),S), -holds(at(P,A),S).
% - il cargo non deve essere in un areoporto
:- occurs(unload(C,P,A),S), holds(in(C,A),S).

% precondizioni di fly
% - il l'aereo deve essere in A1 e non essere in A2
:- occurs(fly(P,A1,A2),S), -holds(at(P,A1),S).

% trick
% lo stesso aereo non puo' eseguire piu' azioni nello stesso livello
:- occurs(fly(P,A1,A2),S), occurs(load(C,P,A1),S), plane(P), airport(A1), airport(A2), cargo(C).
:- occurs(fly(P,A1,A2),S), occurs(unload(C,P,A1),S), plane(P), airport(A1), airport(A2), cargo(C).

% PERSISTENZA
holds(F, S+1) :-
  fluent(F), state(S),
  holds(F,S), not -holds(F,S+1).

-holds(F,S+1) :-
  fluent(F), state(S),
  -holds(F, S), not holds(F, S+1).

% REGOLE CAUSALI

% cargo non e' in nessun aereo, se e' in areoporto
-holds(in(C,P),S) :- cargo(C), airport(A), plane(P), state(S), holds(at(C,A),S).
% cargo e' in plane, cargo non e' in nessun areoporto
-holds(at(C,A),S) :- cargo(C), airport(A), plane(P), state(S), holds(in(C,P),S).
% cargo e' in areoporto, non e' in tutti gli altri
-holds(at(C,A2),S) :- cargo(C), airport(A1), airport(A2), state(S), holds(at(C,A1),S), A1!=A2.
% aereo in areoporto, non e' in tutti gli altri
-holds(at(P,A2),S) :- plane(P), airport(A1), airport(A2), state(S), holds(at(P,A1),S), A1!=A2.

:- not goal.

#hide.
#show occurs/2.
\end{lstlisting}

\subsection{Problema 1}

Questo primo problema di pianificazione \emph{air cargo} è lo stesso presente nel paragrafo 10.1.1 del \emph{Russel\&Norvig}

Lo stato iniziale e il goal del problema sono:

\begin{lstlisting}
cargo(c1).
cargo(c2).
plane(p1).
plane(p2).
airport(jfk).
airport(sfo).

holds(at(c1,sfo),0).
holds(at(c2,jfk),0).
holds(at(p1,sfo),0).
holds(at(p2,jfk),0).

goal:-  holds(at(c1,jfk),lastlev+1),
        holds(at(c2,sfo),lastlev+1).

\end{lstlisting}

Una soluzione ottima, ottenuta assegnando a \emph{lastlevel} il valore 2 é:

\begin{lstlisting}
occurs(load(c1,p1,sfo),0)
occurs(load(c2,p2,jfk),0)
occurs(fly(p1,sfo,jfk),1)
occurs(fly(p2,jfk,sfo),1)
occurs(unload(c1,p1,jfk),2)
occurs(unload(c2,p2,sfo),2)
\end{lstlisting}

il tempo impiegato è \emph{infinitesimo}.

\subsection{Problema 2}

Il secondo problema è stato formulato in modo da ampliare il numero di cargo da spostare.

\begin{lstlisting}
cargo(c1).
cargo(c2).
cargo(c3).
cargo(c4).
cargo(c5).
plane(p1).
plane(p2).
airport(jfk).
airport(sfo).
airport(bari).

holds(at(p1,sfo),0).
holds(at(p2,jfk),0).

holds(at(c1,sfo),0).
holds(at(c2,sfo),0).
holds(at(c3,sfo),0).
holds(at(c4,sfo),0).
holds(at(c5,sfo),0).

% GOAL
goal:-  holds(at(p1,sfo), lastlev+1),
        holds(at(p2,jfk), lastlev+1),
        holds(at(c1,bari),lastlev+1),
        holds(at(c2,bari),lastlev+1),
        holds(at(c3,bari),lastlev+1),
        holds(at(c4,bari),lastlev+1),
        holds(at(c5,bari),lastlev+1).
\end{lstlisting}

Una delle soluzioni ottenute, impostando \emph{lastlev} a 3 è la seguente.

\begin{lstlisting}
occurs(load(c5,p1,sfo),0)
occurs(load(c4,p1,sfo),0)
occurs(load(c3,p1,sfo),0)
occurs(load(c2,p1,sfo),0)
occurs(load(c1,p1,sfo),0)
occurs(fly(p2,jfk,bari),0)
occurs(fly(p2,bari,jfk),1)
occurs(fly(p1,sfo,bari),1)
occurs(unload(c5,p1,bari),2)
occurs(unload(c4,p1,bari),2)
occurs(unload(c3,p1,bari),2)
occurs(unload(c2,p1,bari),2)
occurs(unload(c1,p1,bari),2)
occurs(fly(p2,jfk,bari),2)
occurs(fly(p2,bari,jfk),3)
occurs(fly(p1,bari,sfo),3)
\end{lstlisting}

Come si può notare, dato che non abbiamo inserito un vincolo di carico del plane, l'aereo $P1$ carica e scarica tutti i cargo in uno solo level, il secondo. L'aereo $P2$, inoltre, fa voli a vuoto, non essendo stata specificata nessuna regola di \emph{viaggia a vuoto solo se c'è da caricare un cargo nell'altro areoporto.}

Il tempo, aumentando di un solo livello il grafo, è aumentato di 10millisecondi.

Usando lo stesso esempio, se aumentassimo il livello massimo (\emph{lastlev}) a 30, invece che a 3, il tempo di esecuzione per trovare almeno una soluzione è di 110millisecondi.
